-- Project Lift System SOFTENG 751 - 5/10/2019
-- Written by Buster Major and Nathan Cairns

model LiftSystem

---- CLASSES ----

enum FloorButtonDirection {up, down}
enum Movement {upward, downward, idle}

class Building
attributes
    fireAlarm : Boolean
operations
    engageFireAlarm()
    disengageFireAlarm()
end

class Floor
attributes
    level : Integer
    label : String
    buttons: Set(FloorButtonDirection)
    waitingFor: Movement
operations
    pressFloorButton(direction : FloorButtonDirection)
end

class Lift
attributes
    level : Integer
    doorsClosed : Boolean
    jobMovement : Movement
    jobs : Set(Floor)
    lockDoor : Boolean
    movementLocked : Boolean
operations
    pressLiftButton(label : String)
    openDoor()
    closeDoor()
    engageEmergencyBreak()
    unlockDoor()
    unlockMovement()
    tick()
end

---- ASSOCIATIONS ----

association CanGoToFloor between
    Lift[0..*] role lift
    Floor[0..*] role floorOptions
end

association CanCallLift between
    Floor[0..*] role _floor
    Lift[0..*] role liftOptions
end

association Floors between
    Building[1] role buildingFloorOwner
    Floor[2..*] role floorsInBuilding
end

association Lifts between
    Building[1] role buildingLiftOwner
    Lift[1..*] role liftsInBuilding
end

constraints

---- INVARIANTS ----

context Lift
    inv lockedDoorCantOpen:
        (lockDoor)implies(doorsClosed=true)
    inv cantMoveIfLocked:
        (movementLocked)implies(jobMovement=Movement::idle and jobs=Set{})
    inv isNotIdleThenQueuePopulated:
        ((jobMovement<>Movement::idle)implies(not(jobs->isEmpty()))) and
        ((not(jobs->isEmpty()))implies(jobMovement<>Movement::idle))

context Building
    inv topLevelCanOnlyGoDown :
        floorsInBuilding->forAll(
            floor1 : Floor |
            not(floorsInBuilding->exists(floor2 : Floor | floor2.level > floor1.level))
            implies
            (floor1.buttons=Set{FloorButtonDirection::down})
        )
    inv bottomLevelCanOnlyGoUP :
        floorsInBuilding->forAll(
            floor1 : Floor |
            not(floorsInBuilding->exists(floor2 : Floor | floor2.level < floor1.level))
            implies
            (floor1.buttons=Set{FloorButtonDirection::up})
        )
    inv middleFloorCanGoUpAndDown :
        floorsInBuilding->forAll(
            floor1 : Floor |
            (floorsInBuilding->exists(floor2 : Floor | floor2.level > floor1.level)
                and floorsInBuilding->exists(floor3 : Floor | floor3.level < floor1.level)
            )
            implies
            (floor1.buttons=Set{FloorButtonDirection::down, FloorButtonDirection::up})
        )

---- METHODS ----

-- Floor button actions

context Floor::pressFloorButton(direction : FloorButtonDirection)
    pre buttonIsOnFloor: buttons->includes(direction)
    post movingUpSoAddJobToClosestUpwardsMovingLift:
        let shortlistLifts:Set(Lift) = (liftOptions->select(lift : Lift | (lift.jobMovement@pre)=Movement::upward) -- Lifts moving up
                ->intersection(liftOptions->select(lift : Lift | lift.level <= self.level))) -- Lifts below
                ->union(liftOptions->select(lift : Lift | (lift.jobMovement@pre)=Movement::idle)) -- Idle lifts
        in
        (direction=FloorButtonDirection::up)
        implies
            ((not(shortlistLifts->isEmpty()))implies(
                liftOptions->exists(lift1 : Lift | shortlistLifts->includes(lift1) and
                    not(shortlistLifts->exists(
                        lift2 : Lift | (lift2.level - level).abs() < (lift1.level - level).abs()
                    )) and
                    lift1.jobs->includes(self)
                )
            )) and
            ((shortlistLifts->isEmpty())implies(self.waitingFor=Movement::upward))
    post movingDownSoAddJobToClosestDownwardMovingLift:
        let shortlistLifts:Set(Lift) = (liftOptions->select(lift : Lift | (lift.jobMovement@pre)=Movement::downward) -- Lifts moving down
                ->intersection(liftOptions->select(lift : Lift | lift.level >= self.level))) -- Lifts above
                ->union(liftOptions->select(lift : Lift | (lift.jobMovement@pre)=Movement::idle)) -- Idle lifts
        in
        (direction=FloorButtonDirection::down)
        implies
            ((not(shortlistLifts->isEmpty()))implies(
                liftOptions->exists(lift1 : Lift | shortlistLifts->includes(lift1) and
                    not(shortlistLifts->exists(
                        lift2 : Lift | (lift2.level - level).abs() < (lift1.level - level).abs()
                    )) and
                    lift1.jobs->includes(self)
                )
            )) and
            ((shortlistLifts->isEmpty())implies(self.waitingFor=Movement::downward))

-- Lift button actions

context Lift::pressLiftButton(label : String)
    pre floorLabelExists: floorOptions->exists(_floor : Floor | _floor.label = label)
    pre floorIsAboveIfUpwards:
        (jobMovement=Movement::upward)
        implies
        (floorOptions->exists(_floor: Floor | _floor.label=label and _floor.level >= level))
    pre floorIsBelowIfDownwards:
        (jobMovement=Movement::downward)
        implies
        (floorOptions->exists(_floor: Floor | _floor.label=label and _floor.level <= level))
    post addFloorToJobQueue:
        not(floorOptions->exists(_floor: Floor | _floor.label=label and _floor.level = level))
        implies
        (jobs->exists(_floor : Floor | _floor.label = label and floorOptions->includes(_floor)))
    post onSameLevelOpenDoors:
        ((floorOptions->exists(_floor: Floor | _floor.label=label and _floor.level = level)))
        implies
        (doorsClosed=false)

context Lift::openDoor()
    pre doorNotLocked: lockDoor=false
    post openDoor: doorsClosed=false

context Lift::closeDoor()
    post openDoor: doorsClosed=true

-- Emergency methods

context Lift::engageEmergencyBreak()
    post liftIsStopped : jobMovement=Movement::idle and jobs=Set{}
    post doorIsClosed : doorsClosed=true
    post engageLock : lockDoor=true and movementLocked=true

context Lift::unlockDoor()
    post disengageLock : lockDoor=false

context Lift::unlockMovement()
    post disengageMovementLock : movementLocked=false

context Building::engageFireAlarm()
    post fireAlarmIsOn : fireAlarm=true
    post allLiftsNotMoving :
        liftsInBuilding->forAll(
            lift : Lift | lift.jobMovement=Movement::idle
            and
            lift.jobs=Set{}
        )
    post allLiftDoorsOpen :
        liftsInBuilding->forAll(lift : Lift | lift.lockDoor=false and lift.doorsClosed=false)
    post lockLiftMovement :
        liftsInBuilding->forAll(lift : Lift | lift.movementLocked=true)
    post allFloorWaitingForCleared :
        floorsInBuilding->forAll(_floor : Floor | _floor.waitingFor=Movement::idle)

context Building::disengageFireAlarm()
    post fireAlarmIsOff : fireAlarm=false
    post allLiftsUnlocked :
        liftsInBuilding->forAll(lift : Lift | lift.movementLocked=false)

-- Lift tick

context Lift::tick()
    pre doorsAreClosed:
        doorsClosed=true
    post liftHasMoved:
        let nextJob:Floor = (jobs@pre->any(floor1 : Floor |
            ((jobMovement=Movement::upward)implies(not(jobs@pre->exists(floor2 : Floor | floor1.level > floor2.level))))
            and
            ((jobMovement=Movement::downward)implies(not(jobs@pre->exists(floor2 : Floor | floor1.level < floor2.level))))
        ))
        in
        (nextJob<>null)implies(level = nextJob.level)
    post openDoorsAndRemoveQueueAndSetMovementsIfArrived:
        let nextJob:Floor = (jobs@pre->any(floor1 : Floor |
            ((jobMovement=Movement::upward)implies(not(jobs@pre->exists(floor2 : Floor | floor1.level > floor2.level))))
            and
            ((jobMovement=Movement::downward)implies(not(jobs@pre->exists(floor2 : Floor | floor1.level < floor2.level))))
        )),
            waitingListJob = (floorOptions@pre->any(_floor : Floor | (_floor.waitingFor@pre)<>Movement::idle))
        in
        ((doorsClosed@pre=true and not(jobs@pre->isEmpty()))implies(
            doorsClosed=false and
            ((((jobs@pre)-Set{nextJob})->isEmpty())implies(
                ((waitingListJob=null)implies(jobMovement=Movement::idle and jobs=(jobs@pre)-Set{nextJob}))
                and
                ((waitingListJob<>null)implies(jobMovement=(waitingListJob.waitingFor@pre) and
                    jobs=Set{waitingListJob} and waitingListJob.waitingFor=Movement::idle
                ))
            ))
            and
            ((not(((jobs@pre)-Set{nextJob})->isEmpty()))implies(jobs=(jobs@pre)-Set{nextJob}))
        ))